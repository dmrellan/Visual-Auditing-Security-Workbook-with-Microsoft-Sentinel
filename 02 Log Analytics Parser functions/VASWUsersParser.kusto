//VASWUsersParser
VASW_users_CL 
| summarize arg_max(TimeGenerated,*) by SID_s
| where TimeGenerated >= ago(24h)
| project TimeGenerated
    , SID = SID_s
    , Enabled = Enabled_s
    , pwdLastSet = todatetime(pwdLastSet_s)
    , SamAccountName = SamAccountName_s
    , accountExpires = case( //A value of 0 or 0x7FFFFFFFFFFFFFFF (9223372036854775807) indicates that the account never expires.
                            accountExpires_s=="9223372036854775807", todatetime("12/31/9999 23:59:59.999"), 
                            accountExpires_s=="0", todatetime("01/01/1601"), 
                            todatetime(accountExpires_s))
    , ObjectClass = ObjectClass_s
    , adminCount = adminCount_s
    , lastLogonTimestamp = todatetime(lastLogonTimestamp_s)
    , DistinguishedName = DistinguishedName_s
    , whenCreated = todatetime(whenCreated_s)
    , userAccountControl = userAccountControl_s
    , whenChanged = todatetime(whenChanged_s)
    , Name = Name_s
    , msDSPrincipalName = msDS_PrincipalName_s
    , ObjectGUID = ObjectGUID_g
| extend isLockedOut = iif(binary_and(toint(userAccountControl),16)>0,"True","False")
| extend isDisabled = iif(binary_and(toint(userAccountControl),2)>0,"True","False")
| extend isNoPasswordRequired = iif(binary_and(toint(userAccountControl),32)>0,"True","False")
| extend isPasswordNeverExpires = iif(binary_and(toint(userAccountControl),65536)>0,"True","False")
| extend isTrustAccount = iif(binary_and(toint(userAccountControl),2048)>0,"True","False")
| extend isAdmin = iif(adminCount==1,"True","False")
| extend DaysSinceLastLogon = iif(isnotnull(datetime_diff('day',now(),lastLogonTimestamp)),datetime_diff('day',now(),lastLogonTimestamp),36000)
| extend DaysSincePasswordChanged = iif(isnotnull(datetime_diff('day',now(),pwdLastSet)),datetime_diff('day',now(),pwdLastSet),36000)
| extend DaysSinceAccountExpired = iif(isnotnull(datetime_diff('day',now(),accountExpires)),datetime_diff('day',now(),accountExpires),36000)
| extend isAccountExpired = iif(accountExpires == todatetime("01/01/1601") or accountExpires == todatetime("12/31/9999 23:59:59.999"), "False", iif(DaysSinceAccountExpired > 0, "True","False"))